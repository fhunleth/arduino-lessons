#02â€”Tones and Music

__Introduction:__
The purpose of this exercise is to learn how to make noise on the Arduino and
then to write programs that play music.

##Step 1: Build the circuit and play a tone

1. If the Arduino is connected to a PC, disconnect it.

2. Find a Piezo buzzer. The ones that we have are black cylinders. If there is
a sticker on the top of the buzzer, take it off. Find a black wire and a red
wire.

3. Wire up the circuit like in the following picture. The wires under the Piezo
buzzer can be hard to see, so make sure that the black and red wires are
connected to the right columns on the breadboard. One side of the buzzer has
a plus sign. The pin under the plus should be connected to the red wire and to
pin 11 on the Arduino.

![Alt Circuit](circuit.png)

4. Now connect the Arduino back to the PC using the USB cable.

5. Start a new sketch and enter the following program:

```c
void setup()
{
  tone(11, 440);
  delay(1000);
  noTone(11);
} 

void loop()
{
}
```

6. Save the program as "tone1". Press the `upload` button and listen for a sound. If you
hear something, then good job. It worked!

Now look back at your program. The `tone()` function tells the Arduino to turn
on and off pin 11 (which is connected to the Piezo buzzer) 440 times a second!
Your ears can hear this sound. The `delay()` function then waits for 1000
milliseconds or 1 second. Finally `noTone()` turns the sound off because it
would be really, really annoying if the sound went on forever.

7. Now modify the program by changing the number 440 to another number. What
   happens when you make the number bigger? What are the biggest and smallest
   numbers that you can hear? Which number is the most annoying to the person
   sitting next to you? What if you change your program to move the code from
   the `setup` function to the `loop` function?

##Step 2: Making music

Now that you've had fun annoying people, you should probably make some nicer
sounds on the Arduino. The numbers that you have been picking specify the tone's
frequency. Frequency is measured in Hertz which is often just written as Hz.
If you play a musical instrument, each note that you play has a main frequency.
The following chart lists some notes and their frequencies:

```
    C  262 Hz
    C# 277 Hz
    D  294 Hz
    D# 311 Hz
    E  330 Hz
    F  349 Hz
    F# 370 Hz
    G  392 Hz
    G# 415 Hz
    A  440 Hz
    A# 466 Hz
    B  494 Hz
    C  523 Hz
```
If you are wondering why the Arduino and the Piezo buzzer do not sound as nice
as a piano, it is for a few reasons. First, the Arduino is just playing one
frequency at a time. This makes it sound like a computer. Real instruments play
a few frequencies at the same time even if you're just playing one note. Second,
the Piezo buzzer is not very good. Don't be upset, the Piezo buzzer can still be
fun to use.

1. Let's try to make it play a song now... Well, not quite yet. We need to write
   a function to make playing notes easier. Start a new sketch and enter the
   following code:

```shell
void note(int frequency, int duration)
{
  tone(11, frequency);
  delay(duration);
  noTone(11);
}

void setup()
{
  note(440, 1000);
}

void loop()
{
}
```

The `note()` function is going to help us out a lot. It has two parameters. The
first one is `frequency` and that's the frequency of the note that we want to
play. The second parameter is the duration of the note in milliseconds.

2. Now modify the `setup()` function to play two notes:

```shell
void setup()
{
  note(440, 1000);
  note(440, 1000);
}
```

3. Upload the sketch to the Arduino. Did it play two notes? Do you know what's
   wrong (don't look at the next line)?

4. Let's fix the problem by adding a short pause after playing the note. When
   people play musical instruments they normally do this without thinking about
   it. Modify your `note()` function to look like this:

```shell
void note(int frequency, int duration)
{
  tone(11, frequency);
  delay(duration * 0.9);
  noTone(11);
  delay(duration * 0.1);
}
```

5. Save your sketch and test it out by uploading it to the Arduino. Hopefully
   you hear two notes now.

6. Since it's hard to know how long a note is played in milliseconds, lets add
   some variables to our program so that we can specify each notes duration by
   how many beats long it is. Add the following to the `setup()` function:

```shell
void setup()
{
  int quarter_note = 576;
  int eighth_note = quarter_note / 2;
  int sixteenth_note = eighth_note / 2;
  
  ...
}
```

This says that a quarter note is 576 milliseconds long. An eighth note is
half as long as a quarter note. A sixteenth note is half as long as an eighth note.
Depending on the song, the duration of these notes is longer or shorter. This is
called the tempo of the song. If you want the song to go faster, just change
`quarter_note` to be shorter.

7. Now let's have the Arduino play a song. Change your `setup()` function to
   look like this:

```shell
void setup() 
{
  int quarter_note = 576;
  int eighth_note = quarter_note / 2;
  int sixteenth_note = eighth_note / 2;
  
  note(392, quarter_note);
  note(392, quarter_note);
  note(392, quarter_note);
  note(311, eighth_note + sixteenth_note);
  note(466, sixteenth_note);
  
  note(392, quarter_note);
  note(311, eighth_note + sixteenth_note);
  note(466, sixteenth_note);
  note(392, quarter_note * 2);
}
```

8. Save your sketch and upload it to the Arduino. Did it work?

9. Try experimenting with the song. What happens if you change the duration of
   the quarter note to 288 milliseconds? If you want to enter in more notes,
take a look at the
(music)[http://www.hamienet.com/Imperial-March-sheet-music-page_11186-6-1.png].

Now that you've gotten the Arduino to play a song, try making it play a song
that you like. If you don't know the notes to your song, try searching for sheet
music for it on the Internet.

